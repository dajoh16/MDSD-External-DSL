grammar org.xtext.mdsd.external.QuickCheckApi with org.eclipse.xtext.common.Terminals

generate quickCheckApi "http://www.xtext.org/mdsd/external/QuickCheckApi"

Builder: 
	"TestBuilder" tests+=Test+;

Test:
	"Test" name=ID requests+=(Request)+
;

Request:
	"request" name=ID method=Method url=URL ("PRE:" preconditions=PreLogicExp)? 
	(params=Param)? body=Body? ("STATE:" action=Action) ("POST:"postconditions=PostLogicExp)?
;

Action:
	actionOp=ActionOperator value=STRING | actionOp=ActionOperator
;

ActionOperator:
	{CreateAction} "create" | {DeleteAction} "delete" | {UpdateAction} "update" | {NoAction} "none"
;


Method:
	{GET} "GET" | {POST} "POST" | {PUT} "PUT" | {PATCH} "PATCH" | {DELETE} "DELETE"	
;

URL:
	protocol=Protocol "://" domain=Domain
;

Domain:
	host=Host (":" port=Port)? "/" (uri=URI)? ("/@" requestID=RequestID)?
;

RequestID:
	name=ID
;

URI:
	name=ID("/" path+=PathPart)*	
;

PathPart:
	part=ID	
;

Host:
	hostParts+=ID ("." hostParts+=ID)* | ips+=INT ("." ips+=INT)*
;

Port:
	{Port} value=INT
;

Protocol:
	"http" | "https"	
;


Param:
	"parameters" pairs+=KVPair (";" pairs+=KVPair)*	
;

KVPair:
	key=Key "=" value=Value 
;

Value:
	ID | INT
;

Key:
	name=ID	
;

Body:
	"body" value=STRING
;

PreLogicExp returns Preproposition:
	PreConjunction("OR" {PreDisjunction.left=current} right=PreConjunction)*
;

PreConjunction returns Preproposition:
	Precondition ("AND" {PreConjunction.left=current} right=Precondition)*	
;

Precondition returns Preproposition:
	stateOp=StateOp value=STRING | stateOp=StateOp	
;

PostLogicExp returns Postproposition:
	PostConjunction ("OR" {PostDisjunction.left=current} right=PostConjunction)*
;

PostConjunction returns Postproposition:
	Postcondition ("AND" {PostConjunction.left=current} right=Postcondition)*	
;


Postcondition returns Postproposition:
	{BodyCondition}requestOp=RequestOp requestValue=ResponseBody | {CodeCondition}requestOp=RequestOp statusCode=StatusCode 
;

StatusCode:
	"code" code=INT
;

ResponseBody:
	"body" body=STRING
;

RequestOp:
	{RequestOp} notOp=NotOp? "returns"	
;

StateOp :
	{StateOp} notOp=NotOp? "contains" | {StateOp} notOp=NotOp? "empty"
;

NotOp:
	"not"	
;