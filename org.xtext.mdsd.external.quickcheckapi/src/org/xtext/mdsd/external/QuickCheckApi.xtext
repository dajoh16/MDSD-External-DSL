grammar org.xtext.mdsd.external.QuickCheckApi with org.eclipse.xtext.common.Terminals

generate quickCheckApi "http://www.xtext.org/mdsd/external/QuickCheckApi"

Builder: 
	tests+=Test+;

Test:
	"Test" name=ID variableDefs+=(VariableDef)* requests+=(Request)+
;
VariableDef:
	"var" name=ID "=" variableValue=Json
;

Request:
    "request" name=ID method=Method url=URL ("Precond:" preconditions=PreLogicExp)? 
    (params=Param)? body=Body? ("STATE:" action=Action) ("Postcond:"postconditions=PostLogicExp)?
;


Action:
	{CreateAction} "create" value=JsonUse | {DeleteAction} "delete" value=JsonUse | {UpdateAction} "update" value=JsonUse | {NoAction} "none"
;


Method:
	{GET} "GET" | {POST} "POST" | {PUT} "PUT" | {PATCH} "PATCH" | {DELETE} "DELETE"	
;

URL:
	protocol=Protocol "://" domain=Domain
;

Domain:
	host=Host (":" port=Port)? "/" (uri=URI)? ("/@" requestID=RequestID)?
;

RequestID:
	name=ID
;

URI:
	name=ID("/" path+=PathPart)*	
;

PathPart:
	part=ID	
;

Host:
	hostParts+=ID ("." hostParts+=ID)* | ips+=INT ("." ips+=INT)*
;

Port:
	{Port} value=INT
;

Protocol:
	"http" | "https"	
;


Param:
	"parameters" pairs+=KVPair (";" pairs+=KVPair)*	
;

KVPair:
	key=Key "=" value=Value 
;

Value:
	ID | INT
;

Key:
	name=ID	
;

Body:
	"body" value=JsonUse
;

JsonUse:
	VariableUse | Json
;

VariableUse:
	variable=[VariableDef]
;

Json:
	JsonObject | JsonList
	
;

JsonList returns Json:
	{JsonList} "[" jsonValues+=JsonValue ("," jsonValues+=JsonValue)* "]"
;

JsonObject returns Json:
	{JsonObject} "{" jsonPairs+=JsonPair ("," jsonPairs+=JsonPair)* "}"
;

JsonPair returns Json:
	{JsonPair} key=ID ":" value=JsonValue
;

JsonValue returns Json:
	{StringValue} value=STRING | {IntValue} value=INT |
	{NestedJsonValue} value=JsonObject | {ListJsonValue} value=JsonList |
	{IdValue} value=IdJson
;

IdJson:
	"@Id" idType=IdType
;

IdType:
	{IntType} "int" | {StrType} "str"
;

PreLogicExp returns Preproposition:
	PreConjunction("OR" {PreDisjunction.left=current} right=PreConjunction)*
;

PreConjunction returns Preproposition:
	Precondition ("AND" {PreConjunction.left=current} right=Precondition)*	
;

Precondition returns Preproposition:
	{ContainsCondition} notOp=NotOp? "contains" value=JsonUse | {EmptyCondition} notOp=NotOp? "empty"
;

PostLogicExp returns Postproposition:
	PostConjunction ("OR" {PostDisjunction.left=current} right=PostConjunction)*
;

PostConjunction returns Postproposition:
	Postcondition ("AND" {PostConjunction.left=current} right=Postcondition)*	
;


Postcondition returns Postproposition:
	{BodyCondition}requestOp=RequestOp requestValue=ResponseBody | {CodeCondition}requestOp=RequestOp statusCode=StatusCode 
;

StatusCode:
	"code" code=INT
;

ResponseBody:
	{ResponseBody} "body" body=JsonUse?
;

RequestOp:
	{RequestOp} notOp=NotOp? "returns"	
;

NotOp:
	"not"	
;